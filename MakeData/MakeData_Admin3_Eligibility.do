/************************************************************************************************************************ 
This file creates eligibility for retirement using Administrative Data release at 04.06.2016 ==> pre-survey3 
************************************************************************************************************************/

merge 1:1 individual_id using "${working}Admin3_YOS", nonotes // generated by Makedata_Admin3_YOS, using 04.06.2016 pre-survey3 admin data
keep if _merge ==1 | _merge==3
drop _merge

**************************************
*** Eligible for Normal Retirement 
**************************************


gen admin3_age_at_svy3 = age + ((mdy(05,10,2016)-mdy(03,23,2016))/365.25) // age information are recorded as of 3.23.2016 in admin data. Please refer to notes for detail
lab var admin3_age_at_svy3 "Age as of Survey 3" // First day of fielding survey 3  : 05/10/2016

gen admin3_age_at_svy5 = age + ((mdy(12,01,2017)-mdy(03,23,2016))/365.25)
lab var admin3_age_at_svy5 "Age as of Survey 5" // Survey 5 : 12/01/2016


gen admin3_yos_at_svy3 = admin3_yos_sum_adj + ((mdy(05,10,2016)-mdy(12,31,2015))/365.25) // yos are recorded at the end of 2015 in admin data.
lab var admin3_yos_at_svy3 "YOS as of Survey 3" 

gen admin3_yos_at_svy5 = admin3_yos_sum_adj + ((mdy(12,01,2017)-mdy(12,31,2015))/365.25) 
lab var admin3_yos_at_svy5 "YOS as of Survey 5"


drop admin3_yos_sum-admin3_contryos_curr


foreach x in  svy3 svy5 { 

di "`x'"

gen yos_over30_at`x' = (30 <= (admin3_yos_at_`x')) 

gen yos_over25_age_60_at`x' =  (25 <= (admin3_yos_at_`x')) & admin3_age_at_`x' >=60

gen yos_over5_age_65_at`x' = (5 <= (admin3_yos_at_`x')) & admin3_age_at_`x' >=65 

gen admin3_eligible_normal_at_`x' = max(yos_over30_at`x', yos_over25_age_60_at`x', yos_over5_age_65_at`x')

}


lab var admin3_eligible_normal_at_svy3 "Eligible Normal (Full Benefits) as of Survey 3" //"Normal Eligible as of Survey 3"
lab var admin3_eligible_normal_at_svy5 "Eligible Normal (Full Benefits) as of Survey 5" //"Normal Eligible as of Survey 5"

drop yos_over30_at* yos_over25_age_60_at* yos_over5_age_65_at* age


**************************************
*** Eligible for Early Retirement 
**************************************

foreach x in  svy3 svy5 { 

di "`x'"

gen yos_over20_age_50_at`x' =  (20 <= (admin3_yos_at_`x')) & admin3_age_at_`x' >=50

gen yos_over5_age_60_at`x' = (5 <= (admin3_yos_at_`x')) & admin3_age_at_`x' >=60 

gen admin3_eligible_early_at_`x' = max(yos_over20_age_50_at`x', yos_over5_age_60_at`x')

}


lab var admin3_eligible_early_at_svy3 "Eligible Early (Reduced Benefits) as of Survey 3" //"Early Eligible as of Survey 3"
lab var admin3_eligible_early_at_svy5 "Eligible Early (Reduced Benefits) as of Survey 5" // "Early Eligible as of Survey 5"

drop yos_over20_age_50_at* yos_over5_age_60_at* 


*** Adjusting Eligible for Normal/Early Retirement ==> If Eligible for Normal Retirement, then not Eligible for Early Retirement

foreach x in  svy3 svy5 { 

di "`x'"

tab admin3_eligible_early_at_`x' admin3_eligible_normal_at_`x' 


replace admin3_eligible_early_at_`x'=0 if admin3_eligible_normal_at_`x' ==1 & admin3_eligible_early_at_`x'==1

tab  admin3_eligible_normal_at_`x'  admin3_eligible_early_at_`x'

}


*******************************
*** Not Eligible for Retirement
*******************************

gen admin3_eligible_none_at_svy5 = (admin3_eligible_early_at_svy5 == 0 & admin3_eligible_normal_at_svy5 == 0) // original var name: gen elig_none = elig_early == 0 & elig_norm == 0
lab var admin3_eligible_none_at_svy5 "Not Eligible as of December 2017"


*******************************
*** Past Eligible and Just Eligible
*******************************

gen admin3_just_eligible_normal_svy5 = (admin3_eligible_normal_at_svy5 == 1)*(admin3_eligible_normal_at_svy3 == 0) //just Eligible as of svy5 date. original var name: new_normal_eligible_s5

gen admin3_past_eligible_normal_svy5 = (admin3_eligible_normal_at_svy5 == 1)*(admin3_eligible_normal_at_svy3 == 1) //Eligible as of svy3 and svy5: past_normal_eligible_s5


***************************************
*** Social Security retirement benefits
***************************************

foreach x in  svy3 svy5 { 

di "`x'"

gen admin3_eligible_SS_at_`x' = (admin3_age_at_`x' >=62)

}

lab var admin3_eligible_SS_at_svy3 "Eligible Social Security Retirement Benefits as of Survey 3" //"Early Eligible as of Survey 3"
lab var admin3_eligible_SS_at_svy5 "Eligible Social Security Retirement Benefits as of Survey 5" // "Early Eligible as of Survey 5"


*** Attach notes regarding dataset created by this do file to resulting dta file. 

note:  {break} ///
Definition for Early/Normal Eligibility:  {break} ///
 {space 2} {break} ///
Early Eligible {break} ///
Age 50 and 20 years of service as of December 2017 OR {break} ///
Age 60 and 5 years of service as of December 2017 {break} ///
 {space 2} {break} ///
Normal Eligible {break} ///
Age 65 and 5 years of service as of December 2017 (reached age 65 with 5 years of membership service) OR {break} ///
Age 60 and 25 years of service as of December 2017 (reached age 60 with 25 years of service) OR {break} ///
Any age and 30 years of service as of December 2017 (have completed 30 years of service at any age) 
note: Eligibility is measured at two points in time: May 2016 and December 2017.

note: Survey 3 fielding day 1 : 5/10/2016; Survey 5 date : 12/01/2017



/* 

Note:
Age in survey 3 admin dataset is reported as of 03/23/2016
==> this is confirmed in C:\Users\ariel\Dropbox\Sloan Working Longer RA\Programs and Data\Code\Survey3\Variables definition\s3_age_at_survey_actives


without int


gen admin3_age_at_svy3 = age + ((mdy(05,10,2016)-mdy(03,23,2016))/365.25)
lab var admin3_age_at_svy3 "Age as of Survey 3" // Survey 3 date 1  : 05/10/2016

gen admin3_age_at_svy5 = age + ((mdy(12,01,2017)-mdy(03,23,2016))/365.25)
lab var admin3_age_at_svy5 "Age as of Survey 5" // Survey 5 : 12/01/2016


gen admin3_yos_at_svy3 = admin3_yos_sum_adj + ((mdy(05,10,2016)-mdy(12,31,2015))/365.25)
lab var admin3_yos_at_svy3 "YOS as of Survey 3" 

gen admin3_yos_at_svy5 = admin3_yos_sum_adj + ((mdy(12,01,2017)-mdy(12,31,2015))/365.25) //==> might only keep svy5 eventually
lab var admin3_yos_at_svy5 "YOS as of Survey 5" */


/*gen admin3_yos = admin3_yos_sum_adj + ((mdy(04,06,2016)-mdy(12,31,2015))/365.25)
lab var admin3_yos "YOS as of April 2016"
gen admin3_age = age + ((mdy(04,06,2016)-mdy(03,23,2016))/365.25)
lab var admin3_age "Age as of April 2016"*/




/* with int

gen admin3_age_at_svy3 = age + ((mdy(05,10,2016)-mdy(03,23,2016))/365.25) // data relase date is 04.06.2016. However, after comparing with survey data, it seems age information are recorded as of 3.23.2016
lab var admin3_age_at_svy3 "Age as of Survey 3" // Survey 3 date 1  : 05/10/2016

gen admin3_age_at_svy5 = age + ((mdy(12,01,2017)-mdy(03,23,2016))/365.25)
lab var admin3_age_at_svy5 "Age as of Survey 5" // Survey 5 : 12/01/2016


gen admin3_yos_at_svy3 = admin3_yos_sum_adj + ((mdy(05,10,2016)-mdy(12,31,2015))/365.25) // yos are recorded at the end of 2015
lab var admin3_yos_at_svy3 "YOS as of Survey 3" 

gen admin3_yos_at_svy5 = admin3_yos_sum_adj + ((mdy(12,01,2017)-mdy(12,31,2015))/365.25) //==> might only keep svy5 eventually
lab var admin3_yos_at_svy5 "YOS as of Survey 5" */



/* include different last plan code


gen yos_over5_age_55_at`x' = (5 <= (admin3_yos_at_`x' + (3/12))) & admin3_age_at_`x' >= 55

replace admin3_eligible_normal_at_`x'=max(yos_over30_at`x', yos_over25_age_60_at`x', yos_over5_age_65_at`x', yos_over5_age_55_at`x') if last_plan_code=="STL" | last_plan_code=="LOCL" //Law enforcement officers refer LGERS LEO handbook//

gen yos_over15_age_50_at`x' = (15 <= (admin3_yos_at_`x' + (3/12))) & admin3_age_at_`x' >= 50


replace admin3_eligible_early_at_`x' = max(yos_over20_age_50_at`x', yos_over5_age_60_at`x',yos_over15_age_50_at`x') if last_plan_code=="STL" | last_plan_code=="LOCL" 

replace admin3_eligible_early_at_`x' = max(yos_over20_age_50_at`x', yos_over5_age_60_at`x',yos_over5_age_55_at`x') if last_plan_code=="LOCF" | last_plan_code=="RESG" */



/* Definition of original code from July 2021

	gen ageatsvy_2016=age + ((mdy(05,10,2016)-mdy(03,23,2016))/365.25)
	label var ageatsvy "Age as of survey date (May 10,2016)"

	gen ageatbegin=int(ageatsvy_2016)
	label var ageatbegin "Age as of April 2016"
	
	
	
	gen svy3date=mdy(05,10,2016)
	gen admin3date=mdy(12,31,2015)	
	
	replace yos_2016_adj =yos_2016_adj - ((admin3date-svy3date)/365.25) 
	gen yosatbegin=int(yos_2016_adj)
	label var yosatbegin "YOS as of April 2016"

	
	gen age=int(ageatsvy_2016)
	gen yos=int(yos_2016_adj)   //(imputed_yos_2016)

	gen s3tos5_years=(mdy(12,01,2017)-mdy(05,10,2016))/365.25
	gen ageatsurvey5=int(ageatsvy_2016) + int(s3tos5_years)
	gen yos=int(yos_2016_adj) + int(s3tos5_years) //replace yos_adj_2016 instead of imputed_yos_2016
	gen age=(ageatsurvey5)
	








*/
