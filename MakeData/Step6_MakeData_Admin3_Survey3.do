/*******************************************************************
This file connects survey 3 variables with admin 3 data from April 2016 
And this file would be the final file to generate data we need for our tables
*******************************************************************/

include "MakeData\Step0_SetDirectory"
log using "${log}MakeData_Admin3_Survey3_$date", replace text name("Ariel")


/*******************
*** Merge files
*******************/

*** Import active workers for survey 3

	use "${working}Admin3_Actives", clear // generated by Makedata_Admin3_Actives, using 04.06.2016 pre-survey3 admin data

	
*** Merge job and agency classification (S3 job classification data incomplete, so merge s1 and s3 job classification for more complete job classification) 

	merge 1:1 individual_id using "${working}Admin1_3_JobClassification" ,nonotes // generated by Makedata_Admin3_JobClassification, using pre-survey3 and pre-survey1 admin data
	keep if _merge==3
	drop _merge
	
	
*** Merge salary data 

	merge 1:1 indiv using "${working}Admin3_salary.dta",nonotes // generated by Makedata_Admin3_Salary, using pre-survey3 admin data
	
	keep if _merge==3
	drop _merge
	
	
*** Merge last contribtion date from updated S5 admin release(April 2018) 

	merge 1:1 individual_id using "${raw}s5_population_lastcontrib_13.dta", keepusing(s5_lastcontribdate) 
	keep if _merge==3
	drop _merge
	
	
/**************************************************
*** Define admin variable needed for current tables  
***************************************************

â€» Important Admin Variables:

	Eligibility:
	admin3_eligible_early_at_svy5
	admin3_eligible_normal_at_svy5
	admin3_eligible_early_at_svy3
	admin3_eligible_normal_at_svy3
	admin3_eligible_none_at_svy5
	admin3_just_eligible_normal_svy5
	admin3_past_eligible_normal_svy5	

	Status: 
	admin5_status_not_ret ==>original name: not_ret
	
	Age: 		
	admin3_age : Age as of April 2016 ==> original name: ageatbegin
	admin3_age_at_svy3
	admin3_age_at_svy5
	
 	YOS:	
	admin3_yos : YOS as of April 2016 ==> original name: yosatbegin
	admin3_age_at_svy3
	admin3_yos_at_svy5

	Demographics:
	admin3_female
	
	Job/Agency classification:
	admin3_atype_school 
	admin3_atype_stategovt 
	
	Salary:
	admin3_total_salary_2015*/
************************************************

*** Calculating Eligibility (Eligibility is defined in MakeData_Admin3_Eligibility.do)
	
	include "MakeData\MakeData_Admin3_Eligibility" //include Eligibility code
	

*** Calculating peak value (Peak Value is defined in MakeData_Admin3_PeakValue_undone)

	*include "G:\Shared drives\NCRTS Retirement Timing\Retirement_Timing\Programs\RA\MakeData_Admin3_PeakValue_undone" //include peak value code


*** Identify workers Actively working in Dec 2017 ==> can't use original way to define status_active, bcause only last contribute date is available

	gen admin5_status_not_ret = s5_lastcontribdate >= mdy(12,01,2017) // it is originally named as not_ret
	label var admin5_status_not_ret "Actively Working" // in Dec 2017"	
	
	
*** Female Indicator

	gen admin3_female=gender_code=="F"
	label var admin3_female "Female"	
	
	
*** Redifine Agency Classification
			
	gen admin3_atype_school = admin3_agency_classification == 3 
	lab var admin3_atype_school "Public School Employee"
	
	gen admin3_atype_stategovt = admin3_tsers == 1 & admin3_agency_classification ~= 3
	lab var admin3_atype_stategovt "State Gov't Employee"

/*********************************************
*** Age and Yos restrictions in current paper
**********************************************

Quoted from MorrillPathak_HealthRetirementTiming_v4: 
Recall the sample consists of individuals who were actively working as of April 2016 (admin 3)
and ages 52-64 with at least 5 years of service*/ //==>The sample is drawn from active workers ages 52 to 64 as of April 2016 with five or more years of service as of April 2016.
	
	keep if inrange(admin3_age_at_svy3,52,65) //keep if inrange(admin3_age_at_svy3,52,64)

	drop if admin3_yos_at_svy3 < 5 

*** Drop if last contribution date is mssing or is earlier than April 2016 in Admin 5 data

	gen admin5_contribmth=mofd(s5_lastcontribdate)
	format admin5_contribmth %tm

    drop if admin5_contribmth==.
	
	drop if admin5_contribmth<ym(2016, 4)	
	
*** Adjust Agency Classification
replace admin3_atype_school =. if admin3_agency_classification ==.
replace admin3_atype_stategovt =. if admin3_agency_classification ==.

*drop if admin3_agency_classification == .
	
	
**********************
*** Merge survey variables
**********************	

merge 1:1 individual_id using "${working}Survey3_Demo_Health"
keep if _merge==3
drop _merge

*keep indi admin3* svy3* admin5_status_not_ret //peakdiffa 

notes replace _dta in 1: This file identifies the actives using administrative data release at 04.06.2016(pre-survey3) and connects with survey 3 variables // edit the notes
notes drop _dta in 2
notes drop _dta in 5
notes renumber _dta

******************************************************************
*** drop if missing value for health status, marriage status 
******************************************************************
*** Must have a non-missing value:
*- Marital status
drop if svy3_married == .

*- Health status
drop if svy3_own_health ==.

*- Health status of spouse non-missing if married //3528 before dropping *** Change made at 3/22/2023 
drop if svy3_sphlthpoor == . & svy3_married ==1
replace svy3_sphlthpoor = 0 if svy3_married ==0

*- Agency type (for TSERS) or indicator for LGERS
drop if admin3_tsers==. | admin3_lgers==.
drop if admin3_agency_classification ==. // 6 missing


save "${working}Admin3_Survey3.dta", replace

count if admin3_agency_classification ==. // 6 missing
count if svy3_race_white ==. // 26 missing


log close _all
exit
/*


list individual_id mem_retirement_system_code membership_status_code termination_type_code membership_total_service ba_status_code if _merge==1
exit
/*      +-----------------------------------------------------------------+
        | indivi~d   mem_re~e   membe~de   termi~de   m~tota~e   ba_st~de |
        |-----------------------------------------------------------------|
 37989. | P3051121        ORP   CLOSWITH                     0            |
 37990. | P3051121      TSERS       ACTV                     .            |
        +-----------------------------------------------------------------+*/


