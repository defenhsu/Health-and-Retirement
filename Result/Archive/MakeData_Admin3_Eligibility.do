/************************************************************************************************************************ 
This file creates eligibility for retirement using Administrative Data release at 04.06.2016 ==> pre-survey3 
************************************************************************************************************************/

clear
capture log close _all
set more off
cd "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\Programs"
global date "$S_DATE"
log using "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\Programs\Logs\MakeData_Admin3_Eligibility_$date", replace text name("Ariel")
global raw "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\RawData\"
global working "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\WorkingData\"
************************************************************************

use "${working}Admin3_YOS",clear // generated by Makedata_Admin3_YOS, using 04.06.2016 pre-survey3 admin data

merge 1:1 individual_id using "${raw}ACTIVES_PERSON_INFO.dta", keepusing(age) // 04.06.2016 version
keep if _merge==3 
drop _merge


**************************************
*** Eligible for Normal Retirement 
**************************************

gen admin3_age = age + ((mdy(04,06,2016)-mdy(03,23,2016))/365.25)
lab var admin3_age "Age as of April 2016"

gen admin3_age_at_svy3 = age + ((mdy(05,10,2016)-mdy(03,23,2016))/365.25)
lab var admin3_age_at_svy3 "Age as of Survey 3" // Survey 3 date 1  : 05/10/2016

gen admin3_age_at_svy5 = age + ((mdy(12,01,2017)-mdy(03,23,2016))/365.25)
lab var admin3_age_at_svy5 "Age as of Survey 5" // Survey 5 : 12/01/2016

gen admin3_yos = admin3_yos_sum_adj + ((mdy(04,06,2016)-mdy(12,31,2015))/365.25)
lab var admin3_yos "YOS as of April 2016"

gen admin3_yos_at_svy3 = admin3_yos_sum_adj + ((mdy(05,10,2016)-mdy(12,31,2015))/365.25)
lab var admin3_yos_at_svy3 "YOS as of Survey 3" 

gen admin3_yos_at_svy5 = admin3_yos_sum_adj + ((mdy(12,01,2017)-mdy(12,31,2015))/365.25) //==> might only keep svy5 eventually
lab var admin3_yos_at_svy5 "YOS as of Survey 5" 


foreach x in  svy3 svy5 { 

di "`x'"

gen yos_over30_at`x' = (30 <= (admin3_yos_at_`x' + (3/12))) // 3month window for fuzzy eligibility

gen yos_over25_age_60_at`x' =  (25 <= (admin3_yos_at_`x' + (3/12))) & admin3_age_at_`x' >=60

gen yos_over5_age_65_at`x' = (5 <= (admin3_yos_at_`x' + (3/12))) & admin3_age_at_`x' >=65 

gen admin3_eligible_normal_at_`x' = max(yos_over30_at`x', yos_over25_age_60_at`x', yos_over5_age_65_at`x')

}


lab var admin3_eligible_normal_at_svy3 "Normal Eligible as of May 2016"
lab var admin3_eligible_normal_at_svy5 "Normal Eligible as of December 2017"



**************************************
*** Eligible for Early Retirement 
**************************************

foreach x in  svy3 svy5 { 

di "`x'"

gen yos_over20_age_50_at`x' =  (20 <= (admin3_yos_at_`x' + (3/12))) & admin3_age_at_`x' >=50

gen yos_over5_age_60_at`x' = (5 <= (admin3_yos_at_`x' + (3/12))) & admin3_age_at_`x' >=60 

gen admin3_eligible_early_at_`x' = max(yos_over20_age_50_at`x', yos_over5_age_60_at`x')

}

lab var admin3_eligible_early_at_svy3 "Early Eligible as of May 2016"
lab var admin3_eligible_early_at_svy5 "Early Eligible as of December 2017"



*** Adjusting Eligible for Normal/Early Retirement ==> If Eligible for Normal Retirement, then not Eligible for Early Retirement

foreach x in  svy3 svy5 { 

di "`x'"

tab admin3_eligible_early_at_`x' admin3_eligible_normal_at_`x' 


replace admin3_eligible_early_at_`x'=0 if admin3_eligible_normal_at_`x' ==1 & admin3_eligible_early_at_`x'==1

tab  admin3_eligible_normal_at_`x'  admin3_eligible_early_at_`x'

}



*** Attach notes regarding dataset created by this do file to resulting dta file. 

note: This file creates eligibility for retirement using administrative data release at 04.06.2016 ==> pre-survey ///
note: Raw data used to creates this file: ACTIVES_PERSON_INFO.dta(04.06.2016) 
note: Do file used to creates this file: MakeData_Admin3_Eligibility.do
note:  {break} ///
Definition for Early/Normal Eligibility:  {break} ///
 {space 2} {break} ///
Early Eligible {break} ///
Age 50 and 20 years of service as of December 2017 OR {break} ///
Age 60 and 5 years of service as of December 2017 {break} ///
 {space 2} {break} ///
Normal Eligible {break} ///
Age 65 and 5 years of service as of December 2017 (reached age 65 with 5 years of membership service) OR {break} ///
Age 60 and 25 years of service as of December 2017 (reached age 60 with 25 years of service) OR {break} ///
Any age and 30 years of service as of December 2017 (have completed 30 years of service at any age) 
note: Eligibility is measured at two points in time: April 2016 and December 2017.
note: Survey 3 fielding day 1 : 5/10/2016
note: Survey 5 date : 12/01/2017



*** Keep only relevant variables

keep individual_id admin3_age* admin3_yos-admin3_yos_at_svy5 admin3_eligible_*



save "${working}Admin3_Eligibility_Svy3_Svy5", replace

log close _all

exit

/* Legacy codes

*** Check the results ==> should all be 0 if all indicators defined correctly (timing: admin3 (early/normal) --> svy3 (early/normal) --> svy5 (early/normal) )

*count if admin3_eligible_normal==1 & svy3_eligible_normal==0

*count if admin3_eligible_normal==1 & svy5_eligible_normal==0
count if admin3_eligible_early_at_svy3==1 &  admin3_eligible_normal_at_svy5==0 

*** Check if indicator for reguiremet for retirement is defined correcly

foreach x in  svy3 svy5 { //admin3

gen age_over60_at`x' =1 if admin3_age_at_`x' >=60 
gen yos_over25_at`x' = 1 if (25 <= (admin3_yos_at_`x' + (3/12))) 

tab age_over60_at`x' yos_over25_at`x',m

tab yos_over25_age_60_at`x'

drop age_over60_at`x' yos_over25_at`x'

}

keep individual_id admin3_age* admin3_yos-admin3_yos_at_svy5 admin3_eli*

lab var admin3_eligible_normal_at_svy3  "Eligible for Normal Benefits (Svy3)"
lab var admin3_eligible_early_at_svy3 "Eligible for Reduced Benefits (Svy3)"
lab var admin3_eligible_normal_at_svy5  "Eligible for Normal Benefits (Svy5)"
lab var admin3_eligible_early_at_svy5 "Eligible for Reduced Benefits (Svy5)"



