/************************************************************************************************************************ 
This file creates peak value as of svy5 using NPV.dta and NPV_terminated.dta generated by python
************************************************************************************************************************/

gen age=int(admin3_age_at_svy3) + int((mdy(12,01,2017)-mdy(05,10,2016))/365.25) // keep the original name since we need to merge later
gen yos=int(admin3_yos_at_svy3) + int((mdy(12,01,2017)-mdy(05,10,2016))/365.25) // the yos and age definiation not straightforward, but if not doing so, could not merge with the NPV file

**********************************
*** Define bjective retirement age
**********************************
** Calculate expected retirement age based on age and years of service 

// Normal Retirement //

gen yrsuntil30= 30-yos 
gen yrsuntil60and25yos= max((60- age),(25-yos)) 
gen yrsuntil65and5yos=max((65- age),(5-yos)) 

gen objretyrs=min(yrsuntil30, yrsuntil60and25yos, yrsuntil65and5yos)

gen yrsuntil55and5yos=max((55- age),(5-yos)) 
replace objretyrs=min(objretyrs, yrsuntil55and5yos) if last_plan_code=="STL" | last_plan_code=="LOCL" //Law enforcement officers refer LGERS LEO handbook//


gen normal_objretage=age + objretyrs - (3/12) // 3month window for fuzzy eligibility


// Early retirement //

gen yrsuntil50and20yos=max((50- age),(20-yos)) 
gen yrsuntil60and5yos=max((60- age),(5-yos)) 

gen early_objretyrs=min(yrsuntil50and20yos, yrsuntil60and5yos)

gen yrsuntil50and15yos=max((50- age),(15-yos)) 
replace early_objretyrs=min(early_objretyrs, yrsuntil50and15yos) if last_plan_code=="STL" | last_plan_code=="LOCL" 

gen early_yrsuntil55and5yos=max((55- age),(5-yos)) 
replace early_objretyrs=min(early_objretyrs, early_yrsuntil55and5yos) if last_plan_code=="LOCF" | last_plan_code=="RESG" 

gen early_objretage=age + early_objretyrs-3/12 // 3month window for fuzzy eligibility



********************
*** Calculating NPV
********************

merge 1:1 individual_id age yos using "${working}NPV.dta"
	keep if _merge==3 | _merge ==1
	drop _merge

	qui destring npv*, replace ignore("*")
	gen NPVcurrentage=.
	forvalues i=50/100{
	replace NPVcurrentage=npv`i' if age==`i'
	}

	egen peakvalue=rowmax(npv50-npv100)  //This occurs at normal eligibility//
	gen postnormal=0

	forvalues i=50/99 {
	local j=`i'+1
	replace postnormal=npv`j' if int(normal_objretage)==`i'
	replace postnormal=npv`j' if age==`i' & postnormal==. & admin3_eligible_normal_at_svy5==1
	}
	
	replace peakvalue=postnormal if admin3_eligible_normal_at_svy5==1 //For those past normal eligibility, keep PDV of working another year//
	drop npv*

	merge 1:1 individual_id age yos using "${working}NPV_terminated.dta", keepusing(indiv npv60)
	keep if _merge==3 | _merge ==1
	drop _merge

	qui destring npv60, replace ignore("*")
	replace NPVcurrent=npv60 if admin3_eligible_early_at_svy3==0
	sum peakvalue

	gen peakdiff=peakvalue-NPVcurrent
	sum peakdiff
	sum peakdiff, det

	replace peakdiff=peakdiff/10000
	label var peakdiff "Peak value incentive(0000's)"
	gen peakdiffa = peakdiff*10000
