/*******************************************************************
This file survey 3 variables using survey 3 response from May 2016 
*******************************************************************/

clear
capture log close _all
set more off

cd "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\Programs"
global date "$S_DATE"
log using "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\Programs\Logs\MakeData_Admin3_Survey3_$date", replace text name("Ariel")
global raw "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\RawData\"
global working "G:\My Drive\NCRTS Retirement Timing\Retirement_Timing\WorkingData\"

/*******************
*** Merge files
*******************/

*** Import active workers for survey 3

	use "${working}Admin3_Actives", clear // generated by Makedata_Admin3_Actives, using 04.06.2016 pre-survey3 admin data



*** Merge YOS, age and Eligibility for retirement generated

	merge 1:1 individual_id using "${working}Admin3_Eligibility_Svy3_Svy5" // generated by Makedata_Admin3_Eligibility, using 04.06.2016 pre-survey3 admin data
	keep if _merge==3 // the only unmatched individual, P3051121, his membership is either withdrawed or with missing YOS==> drop it, only keep matched data  
	drop _merge

	
	
*** Merge job and agency classification (S3 job classification data incomplete, so merge s1 and s3 job classification for more complete job classification) 

	merge 1:1 individual_id using "${working}Admin1_3_JobClassification" // generated by Makedata_Admin3_JobClassification, using pre-survey3 and pre-survey1 admin data
	keep if _merge==3
	drop _merge
	

	
*** Merge salary data 

	merge 1:1 indiv using "${working}Admin3_salary.dta" // generated by Makedata_Admin3_Salary, using pre-survey3 admin data
	
	keep if _merge==3
	drop _merge
	
	
*** Merge last contribtion date from updated S5 admin release(April 2018) 

	merge 1:1 individual_id using "${raw}s5_population_lastcontrib_13.dta", keepusing(s5_lastcontribdate) //This is the dataset Siyan put into dropbox, I'm not sure of the source file
	keep if _merge==3
	drop _merge
	
	
/*********************************************
*** Age and Yos restrictions in current paper
**********************************************

Quoted from MorrillPathak_HealthRetirementTiming_v4: 
Recall the sample consists of individuals who were actively working as of April 2016 (admin 3)
and ages 52-64 with at least 5 years of service*/ //==>The sample is drawn from active workers ages 52 to 64 as of April 2016 with five or more years of service as of April 2016.
	
	keep if inrange(admin3_age,52,65) //keep if inrange(admin3_age_at_svy3,52,65)

	drop if admin3_yos < 5
	
	

	gen admin5_contribmth=mofd(s5_lastcontribdate)
	format admin5_contribmth %tm

    drop if admin5_contribmth==.
	drop if admin5_contribmth<ym(2016, 4)
	
	
/**************************************************
*** Define admin variable needed for current tables  
***************************************************

※ Important Admin Variables:

	Eligibility:
	admin3_eligible_early_at_svy5
	admin3_eligible_early_at_svy5
	admin3_eligible_early_at_svy3
	admin3_eligible_early_at_svy3
	admin3_eligible_none_at_svy5
	
	Status: 
	admin5_status_active ==>not_ret
	
	Age: 		
	admin3_age : Age as of April 2016 ==> ageatbegin
	admin3_age_at_svy5
	
 	YOS:	
	admin3_yos : YOS as of April 2016 ==> yosatbegin
	admin3_yos_at_svy5

	Demographics:
	admin3_female
	
	Job/Agency classification:
	admin3_atype_school 
	admin3_atype_stategovt 
	
	Salary:
	admin3_total_salary_2015
	
	
※ In addition, to generate survey variables, need individual_id admin3_tsers and admin3_lgers*/
************************************************
	
include "G:\Shared drives\NCRTS Retirement Timing\Retirement_Timing\Programs\RA\MakeData_Admin3_Eligibility2"
	
	
*** Identify workers Actively working in Dec 2017 ==> can't use original way to define status_active, bcause only last contribute date is available

	gen admin5_status_active = s5_lastcontribdate >= mdy(12,01,2017) // it is originally named as not_ret
	label var admin5_status_active "Actively working in Dec 2017"	
	
	
*** Female Indicator

	gen admin3_female=gender_code=="F"
	label var admin3_female "Female"	
	
	
*** Redifine Agency Classification
			
	gen admin3_atype_school = admin3_job_classification == 3 
	lab var admin3_atype_school "Agency Type: Public School"
	
	gen admin3_atype_stategovt = admin3_tsers == 1 & admin3_job_classification ~= 3
	lab var admin3_atype_stategovt "Agency Type: State Government"

	
*** Not Eligible for Retirement 

	gen admin3_eligible_none_at_svy5 = (admin3_eligible_early_at_svy5 == 0 & admin3_eligible_normal_at_svy5 == 0) // gen elig_none = elig_early == 0 & elig_norm == 0
	lab var admin3_eligible_none_at_svy5 "Not Eligible as of December 2017"
	
	
	
*** Keep important admin variables

	keep admin3_eligible_* admin5_status_active admin3_age admin3_yos admin3_age_at_svy5 admin3_yos_at_svy5 admin3_female admin3_atype_*  admin3_total_salary_2015 ///
	individual_id admin3_tsers admin3_lgers admin3_age_at_svy3

	
**********************
*** Define survey variables
**********************	

merge 1:1 individual_id using "${working}Survey3_Demo_Health"
keep if _merge==3
drop _merge


**** Heath X Eligibility

	gen interact_early_poor = admin3_eligible_early_at_svy5*own_health_poor //==> elig_earlyxpoor
	gen interact_early_good = admin3_eligible_early_at_svy5*(1-own_health_poor) //==>elig_earlyxgood
	gen interact_normal_poor = admin3_eligible_normal_at_svy5*own_health_poor //==elig_normxpoor
	gen interact_normal_good = admin3_eligible_normal_at_svy5*(1-own_health_poor) //==>elig_normxgood
	

*** EHI X Eligibility
	gen interact_early_ehi = admin3_eligible_early_at_svy5*sp_resp_ehi
	gen interact_norm_ehi = admin3_eligible_normal_at_svy5*sp_resp_ehi
	gen interact_early_notehi = admin3_eligible_early_at_svy5*(1-sp_resp_ehi)
	gen interact_norm_notehi = admin3_eligible_normal_at_svy5*(1-sp_resp_ehi)	

	
*** Life expectation X Eligibility
	gen interact_early_low_le = low_life_expect*admin3_eligible_early_at_svy5
	gen interact_norm_low_le = low_life_expect*admin3_eligible_normal_at_svy5
	gen interact_early_high_le = (1-low_life_expect)*admin3_eligible_early_at_svy5
	gen interact_norm_high_le = (1-low_life_expect)*admin3_eligible_normal_at_svy5
	
	
*** Health insurance importance X Eligibility 
	gen interact_early_import = health_ins_import*admin3_eligible_early_at_svy5
	gen interact_norm_import = health_ins_import*admin3_eligible_normal_at_svy5
	gen interact_early_notimport = (1-health_ins_import)*admin3_eligible_early_at_svy5
	gen interact_norm_notimport = (1-health_ins_import)*admin3_eligible_normal_at_svy5

	
keep indi ///
admin5_status_active ///
admin3_eligible_early_at_svy5  ///elig_early 
admin3_eligible_normal_at_svy5 ///elig_normal 
admin3_eligible_none_at_svy5 ///
admin3_eligible_early_at_svy3  ///
admin3_eligible_normal_at_svy3 ///
admin3_age ///
admin3_yos ///
admin3_age_at_svy3 ///
admin3_age_at_svy5 ///
admin3_yos_at_svy5 ///
admin3_atype_school  ///
admin3_atype_stategovt  ///
admin3_total_salary_2015 ///
admin3_female	 ///
own_health_poor  ///
spouse_health_poor  ///
married_p  ///
nkids12  ///
nkids34  ///
nkidsmis  ///
race_blk  ///
race_hisp  ///
race_other  ///
educ_ba  ///
interact_early_poor ///
interact_normal_poor ///
interact_early_good ///
interact_normal_good ///
interact_early_ehi  ///
interact_norm_ehi  ///
interact_early_notehi ///
interact_norm_notehi ///
interact_early_low_le ///
interact_norm_low_le  ///
interact_early_high_le  ///
interact_norm_high_le  ///
interact_early_import  ///
interact_norm_import  ///
interact_early_notimport  ///
interact_norm_notimport  ///
worried  

save "${working}Admin3_Survey3.dta", replace


log close _all
exit
/*


list individual_id mem_retirement_system_code membership_status_code termination_type_code membership_total_service ba_status_code if _merge==1
exit
/*      +-----------------------------------------------------------------+
        | indivi~d   mem_re~e   membe~de   termi~de   m~tota~e   ba_st~de |
        |-----------------------------------------------------------------|
 37989. | P3051121        ORP   CLOSWITH                     0            |
 37990. | P3051121      TSERS       ACTV                     .            |
        +-----------------------------------------------------------------+*/


