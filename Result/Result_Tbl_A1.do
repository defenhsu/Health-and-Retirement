/*******************************************************************
This file generate Appendix Table A1 that compares the working data Admin3_Survey3 to the administrative records
And review data restriction
*******************************************************************/

capture file close _all
include "Step0_SetDirectory"
log using "${log}ResultAppendix_TableA1_$date", replace text name("Ariel")

/*******************
*** Merge files
*******************/

*** Import active workers for survey 3

	use "${working}Admin3_Actives", clear // generated by Makedata_Admin3_Actives, using 04.06.2016 pre-survey3 admin data

	
*** Merge job and agency classification (S3 job classification data incomplete, so merge s1 and s3 job classification for more complete job classification) 

	merge 1:1 individual_id using "${working}Admin1_3_JobClassification" ,nonotes // generated by Makedata_Admin3_JobClassification, using pre-survey3 and pre-survey1 admin data
	keep if _merge==3
	drop _merge
	
	
*** Merge salary data 

	merge 1:1 indiv using "${working}Admin3_salary.dta",nonotes // generated by Makedata_Admin3_Salary, using pre-survey3 admin data	
	keep if _merge==3
	drop _merge
	
	
*** Merge last contribtion date from updated S5 admin release(April 2018) 

	merge 1:1 individual_id using "${raw}s5_population_lastcontrib_13.dta", keepusing(s5_lastcontribdate) 
	keep if _merge==3
	drop _merge
	

*** Calculating Eligibility (Eligibility is defined in MakeData_Admin3_Eligibility.do)
	
	include "MakeData_Admin3_Eligibility" //include Eligibility code
	

*** Identify workers Actively working in Dec 2017 ==> can't use original way to define status_active, bcause only last contribute date is available

	gen admin5_status_not_ret = s5_lastcontribdate >= mdy(12,01,2017) // it is originally named as not_ret
	gen not_working = 1-admin5_status_not_ret	
	label var not_working "Not Working" // in Dec 2017"
	
*** Female Indicator

	gen admin3_female=gender_code=="F"
	label var admin3_female "Female"	
	
	
*** Redifine Agency Classification
			
	gen admin3_atype_school = admin3_agency_classification == 3 
	lab var admin3_atype_school "Public School Employee"
	
	gen admin3_atype_stategovt = admin3_tsers == 1 & admin3_agency_classification ~= 3
	lab var admin3_atype_stategovt "State Gov't Employee"

/*admin3_agency_classification: 131 mssing */
*** adjust Agency Classification

replace admin3_atype_school =. if admin3_agency_classification ==.
replace admin3_atype_stategovt =. if admin3_agency_classification ==.

*drop if admin3_agency_classification == .

/*********************************************
*** Age and Yos restrictions in current paper
**********************************************

Quoted from MorrillPathak_HealthRetirementTiming_v4: 
Recall the sample consists of individuals who were actively working as of April 2016 (admin 3)
and ages 52-64 with at least 5 years of service*/ //==>The sample is drawn from active workers ages 52 to 64 as of April 2016 with five or more years of service as of April 2016.
	
	keep if inrange(admin3_age_at_svy3,52,65) //keep if inrange(admin3_age_at_svy3,52,64)

	drop if admin3_yos_at_svy3 < 5 
	

*** Drop if last contribution date is mssing or is earlier than April 2016 in Admin 5 data

	gen admin5_contribmth=mofd(s5_lastcontribdate)
	format admin5_contribmth %tm

    drop if admin5_contribmth==.
	
	drop if admin5_contribmth<ym(2016, 4)	
	
	
/*So far we have the full admin data that we used in the paper (not yet merge with survey variables) */	

*** Check missing value for each variable

foreach var of varlist admin3_tsers-admin3_atype_stategovt {
qui count if `var' ==.
if r(N)>0 {
di "`var'"
di r(N)
}
}



***************************************************
*** Summary statistics: full administrative records 
***************************************************	

lab var admin3_age_at_svy3 "Age as of Survey 3"
lab var admin3_yos_at_svy3 "YOS as of Survey 3" 

lab var admin3_eligible_early_at_svy3 "Eligible Early (Reduced Benefits) by May 2016"
lab var admin3_eligible_normal_at_svy3 "Eligible Normal (Full Benefits) by May 2016"

gen admin3_newly_early_elig = ( admin3_eligible_early_at_svy3==0 & admin3_eligible_early_at_svy5==1 )
lab var admin3_newly_early_elig "Newly Eligible Early"
gen admin3_newly_normal_elig = ( admin3_eligible_normal_at_svy3==0 & admin3_eligible_normal_at_svy5==1 )
lab var admin3_newly_normal_elig "Newly Eligible Normal"

gen admin3_still_early_elig = ( admin3_eligible_early_at_svy3==1 & admin3_eligible_early_at_svy5==1 )
lab var admin3_eligible_early_at_svy5 "Eligible Early (Reduced Benefits)"
gen admin3_still_normal_elig = ( admin3_eligible_normal_at_svy3==1 & admin3_eligible_normal_at_svy5==1 )
lab var admin3_eligible_normal_at_svy5 "Eligible Normal (Full Benefits)"

lab var admin3_eligible_none_at_svy5 "Not Eligible for Benefits"

local measure_2016 "admin3_age_at_svy3 admin3_yos_at_svy3 admin3_female admin3_atype_school admin3_atype_stategovt admin3_total_salary_10K_2015 admin3_eligible_early_at_svy3 admin3_eligible_normal_at_svy3"   
*local measure_2016 "svy3_own_health_poor svy3_sphlthpoor svy3_married_partner admin3_female admin3_int_age admin3_int_yos svy3_num_kids_12 svy3_num_kids_34 svy3_num_kids_miss svy3_race_black svy3_race_hisp svy3_race_other svy3_educ_ba admin3_atype_school admin3_atype_stategovt admin3_total_salary_10K_2015 admin3_eligible_early_at_svy3 admin3_eligible_normal_at_svy3"
local measure_2017 "not_working admin3_eligible_none_at_svy5  admin3_eligible_early_at_svy5 admin3_eligible_normal_at_svy5"

estpost tabstat  `measure_2016' `measure_2017' , statistics(mean sd) columns(statistics)
mat m_full =e(mean)
mat s_full =e(sd)
qui mean `measure_2016' `measure_2017'
 
foreach var of local measure_2016 {
local v = substr("`var'",8,.) + "_" + "full"
global `v' = _b[`var'] 
di $`v'
macro list `v'
}


foreach var of local measure_2017 {
local v = substr("`var'",8,.) + "_" + "full"
global `v' = _b[`var'] 
}

count
global N_full = r(N)


**********************
*** Merge survey variables
**********************	

merge 1:1 individual_id using "${working}Survey3_Demo_Health"
keep if _merge==3
drop _merge

******************************************************************
*** drop if missing value for health status, marriage status 
******************************************************************
*** Must have a non-missing value:
*- Marital status
drop if svy3_married == .

*- Health status
drop if svy3_own_health ==.

*- Health status of spouse non-missing if married //3528 before dropping *** Change made at 3/22/2023 
drop if svy3_sphlthpoor == . & svy3_married ==1

*- Agency type (for TSERS) or indicator for LGERS
drop if admin3_tsers==. | admin3_lgers==.
drop if admin3_agency_classification ==. // 6 missing



foreach var of varlist svy3_married-svy3_sphlthpoor {
qui count if `var' ==.
if r(N)>0 {
di "`var'"
di r(N)
}
}


***************************************************
*** Summary statistics: Analysis Sample
***************************************************	

estpost tabstat  `measure_2016' `measure_2017' , statistics(mean sd) columns(statistics)
mat m_sample =e(mean)
mat s_sample =e(sd)
qui mean `measure_2016' `measure_2017'

foreach var of local measure_2016 {
local v = substr("`var'",8,.) + "_" + "rest"
global `v' = _b[`var'] 
di $`v'
macro list `v'
}

foreach var of local measure_2017 {
local v = substr("`var'",8,.) + "_" + "rest"
global `v' = _b[`var'] 
}


count
global N = r(N)


*** generating table

file open tbl using "${output}Appendix_TblA1.tex", write replace
file write tbl "\begin{table}[h!]\centering" _n "\caption{Summary statistics: All Administrative Records and Analysis Sample}" _n "\def\sym#1{\ifmmode^{#1}\else\(^{#1}\)\fi}" 

file write tbl "\begin{tabular}{l*{4}{c}}" _n
file write tbl "\toprule"  _n
*file write tbl " & (1) & (2)  \\ " _n
*file write tbl " & Full Administrative Sample   & Analysis Sample   \\ " _n
file write tbl " & \multicolumn{2}{c}{Analysis} & \multicolumn{2}{c}{All } \\" _n
file write tbl " & \multicolumn{2}{c}{Sample} & \multicolumn{2}{c}{Admin Records} \\" _n
file write tbl "& Mean & SD & Mean & SD \\" _n
file write tbl "& \multicolumn{2}{c}{(1)}   & \multicolumn{2}{c}{(2)} \\" _n
file write tbl "\midrule " _n  //"[1em] " _n
file write tbl "  \multicolumn{5}{l}{\textbf{Measured May 2016}}\\" _n //"[1em] " _n

foreach var of local measure_2016 {
local x : variable label `var'
local v_full = substr("`var'",8,.) + "_" + "full"
local v_rest = substr("`var'",8,.) + "_" + "rest"
*file write tbl "`x' & " %4.3f ($`v_full')  "&" %4.3f ($`v_rest')   "\\"  _n 
file write tbl "`x' & " %4.3f (m_sample[1,colnumb(m_sample,"`var'")])  "&" %4.3f (s_sample[1,colnumb(s_sample,"`var'")]) 
file write tbl "&" %4.3f (m_full[1,colnumb(m_full,"`var'")]) "&" %4.3f (s_full[1,colnumb(s_full,"`var'")])  "\\"  _n 
}

file write tbl "[1em] " _n " \multicolumn{5}{l}{\textbf{Measured December 2017}}\\" _n //"[1em] " _n


foreach var of local measure_2017 {
local x : variable label `var'
local v_full = substr("`var'",8,.) + "_" + "full"
local v_rest = substr("`var'",8,.) + "_" + "rest"
*file write tbl "`x' & " %4.3f ($`v_full')  "&" %4.3f ($`v_rest')   "\\"  _n 
file write tbl "`x' & " %4.3f (m_sample[1,colnumb(m_sample,"`var'")])  "&" %4.3f (s_sample[1,colnumb(s_sample,"`var'")]) 
file write tbl "&" %4.3f (m_full[1,colnumb(m_full,"`var'")]) "&" %4.3f (s_full[1,colnumb(s_full,"`var'")])  "\\"  _n 
}


file write tbl "\midrule " _n "Observations   & \multicolumn{2}{c}{"  %4.0f ($N)  "} & \multicolumn{2}{c}{ " %4.0f ($N_full)  "} \\" _n

file write tbl  "\bottomrule" _n "\end{tabular}" _n  "\end{table}" _n "" _n "" _n "" _n "" _n "" _n
file close tbl

log close _all
